buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}
apply plugin: 'android'
apply plugin: 'crashlytics'

repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 10
        versionName "1.0.6-SNAPSHOT"
    }
    productFlavors {
        scout {
            packageName = 'com.example.scout'
            buildConfigField 'String', 'EXAMPLE_API_ROOT', '"http://127.127.127.127:9005"'
        }
        prod {
            buildConfigField 'String', 'EXAMPLE_API_ROOT', '"https://example.com"'
        }
        mock {
            packageName = 'com.example.mock'
            buildConfigField 'String', 'EXAMPLE_API_ROOT', '"not_using_network"'
        }
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    if (project.hasProperty("STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(STORE_FILE)
                storePassword STORE_PASSWORD
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    provided 'pl.mg6.hrisey:hrisey:0.2.0'
    compile 'com.android.support:support-v13:19.0.1'
    compile 'com.squareup.dagger:dagger:1.2.1'
    provided 'com.squareup.dagger:dagger-compiler:1.2.1'
    compile 'com.squareup.okhttp:okhttp:1.5.3'
    compile 'com.squareup.retrofit:retrofit:1.5.0'
    compile 'com.squareup.picasso:picasso:2.2.0'
    compile 'de.greenrobot:eventbus:2.2.0'
    compile(project(':HoloColorPicker'))
    compile 'de.keyboardsurfer.android.widget:crouton:1.8.4'
    compile 'com.crashlytics.android:crashlytics:1.+'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.1'
}
